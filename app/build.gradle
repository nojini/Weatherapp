apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jlleitschuh.gradle.ktlint'
apply plugin: 'dagger.hilt.android.plugin'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jlleitschuh.gradle:ktlint-gradle:10.1.0"
    }
}

def STRING = 'String'
def BUILD_TIME = 'BUILD_TIME'


repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.google.com' }
    mavenCentral()
}

android {
    compileSdkVersion Configs.compileSdkVersion
    buildToolsVersion Configs.buildToolsVersion

    defaultConfig {
        applicationId Configs.applicationId
        minSdkVersion Configs.minSdkVersion
        targetSdkVersion Configs.targetSdkVersion
        versionCode Configs.versionCode
        versionName Configs.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField STRING, BUILD_TIME, "\"${Configs.buildTime}\""

        setProperty("archivesBaseName", "${Configs.archivesBaseName}")
    }
    buildTypes {
        release {
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testCoverageEnabled false

            signingConfig signingConfigs.debug
        }
        debug {
            ext.enableCrashlytics = false
            testCoverageEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/atomicfu.kotlin_module'
        exclude 'META-INF/rxjava.properties'
    }
    buildFeatures {
        buildConfig = true
        dataBinding = true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kapt {
        correctErrorTypes = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // kotlin
    implementation Dependency.kotlin
    implementation Dependency.kotlinReflect

    // android supports
    implementation Dependency.supportv4
    implementation Dependency.appCompat
    implementation Dependency.materialDesign
    implementation Dependency.recyclerView
    implementation Dependency.cardView
    implementation Dependency.coreKtx

    // architecture components
    implementation Dependency.navigationUI
    implementation Dependency.navigationFragment
    implementation Dependency.lifecycleExt
    kapt Dependency.lifecycleAnnotation
    implementation Dependency.liveDataKtx
    implementation Dependency.lifecycleViewModelKtx
    implementation Dependency.roomRuntime
    kapt Dependency.roomCompiler

    // di
    implementation Dependency.hilt
    kapt Dependency.hiltKapt

    // rx
    implementation Dependency.rxJava
    implementation Dependency.rxKotlin
    implementation Dependency.rxAndroid
    implementation Dependency.rxAnimations

    // network
    implementation Dependency.retrofit
    implementation Dependency.okHttp
    implementation Dependency.moshi
    implementation Dependency.moshiConverter
    implementation Dependency.moshiKotlin
    kapt Dependency.moshiKapt
    implementation Dependency.rxJavaAdapter

    // image
    implementation Dependency.picasso

    // debug
    implementation Dependency.stethoCore
    implementation Dependency.stethoOkHttp
    implementation Dependency.stethoUrlConnection
    implementation Dependency.timber

    // shadow view
    implementation Dependency.shadowView

    // date & time
    implementation Dependency.threetenabp

    // search
    implementation Dependency.algolia

    // test
    testImplementation Dependency.junit
    testImplementation Dependency.junitTestExt
    androidTestImplementation Dependency.androidxTestRunner
    androidTestImplementation Dependency.androidxTestRules
    androidTestImplementation Dependency.espressoCore
    testImplementation Dependency.truthExt
    testImplementation Dependency.truth
    testImplementation Dependency.mockK
    testImplementation Dependency.coreTesting
    testImplementation Dependency.robolectric
}

ktlint {
    android = true
    outputToConsole = true
    ignoreFailures = true
    enableExperimentalRules = true
    version = Versions.klintVersion
    kotlinScriptAdditionalPaths {
        include fileTree("scripts/")
    }
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
        include("**/java/**")
    }
}

apply plugin: 'com.google.gms.google-services'
